plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id "com.gradle.plugin-publish" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group 'nl.elec332.gradle'
version '1.0.0-Alpha'

sourceCompatibility = targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    merge
    compileOnly.extendsFrom(merge)
}

dependencies {
    merge "nl.elec332.lib:gradle-utils:1.0.3-Beta"
    compileOnly gradleApi()
}

shadowJar {
    setConfigurations Arrays.asList(project.configurations.merge)
    minimize()
    archiveClassifier = ""
    relocate "nl.elec332.gradle.util", "gnp.embed.nl.elec332.gradle.util"
}

tasks.jar.dependsOn(shadowJar)
tasks.jar.enabled = false

gradlePlugin {

    plugins {
        cppApplicationPlugin {
            id = 'nl.elec332.gradle.cppapplication'
            implementationClass = 'nl.elec332.gradle.nativeplugin.cppproject.CppApplicationPlugin'
            displayName = 'Gradle Native Application Development Plugin'
            description = "A Gradle plugin to simplify application development of C and C++"
        }
        cppLibraryPlugin {
            id = 'nl.elec332.gradle.cpplibrary'
            implementationClass = 'nl.elec332.gradle.nativeplugin.cppproject.CppLibraryPlugin'
            displayName = 'Gradle Native Library Development Plugin'
            description = "A Gradle plugin to simplify library development of C and C++"
        }
        cppTestPlugin {
            id = 'nl.elec332.gradle.cpptest'
            implementationClass = 'nl.elec332.gradle.nativeplugin.cppproject.CppTestPlugin'
            displayName = 'Gradle Native Tests Plugin'
            description = "A Gradle plugin to simplify testing C and C++ projects"
        }
        cmakePlugin {
            id = 'nl.elec332.gradle.cmake'
            implementationClass = 'nl.elec332.gradle.nativeplugin.cmake.CMakeProjectPlugin'
            displayName = 'Gradle CMake Project Plugin'
            description = "A Gradle plugin to wrap CMake projects as a Gradle project"
        }
        javaCppPlugin {
            id = 'nl.elec332.gradle.javacpp'
            implementationClass = 'nl.elec332.gradle.nativeplugin.javacpp.JavaCppPlugin'
            displayName = 'Gradle JavaCpp Project Plugin'
            description = "A Gradle plugin to create JavaCpp projects with gradle"
        }
    }

}

pluginBundle {
    website = 'https://github.com/Elec332/GradleJavaCppPlugin'
    vcsUrl = 'https://github.com/Elec332/GradleJavaCppPlugin.git'
    tags = ['java', 'native', 'cpp', 'javacpp']
}